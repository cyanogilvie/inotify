# vim: ft=tcl foldmethod=marker foldmarker=<<<,>>> shiftwidth=4 ts=4

if {[lsearch [namespace children] ::tcltest] == -1} {
	package require tcltest 2.2.5
	namespace import ::tcltest::*
}


set tmpdir [makeDirectory tmp]

package require inotify

test basic-1.1 {Add and remove a watch} -setup { #<<<
	set queue	[inotify::create_queue]
} -body {
	set wd	[inotify::add_watch $queue $tmpdir {IN_ALL_EVENTS}]
	inotify::rm_watch $queue $wd
} -cleanup {
	if {[info exists queue]} {close $queue; unset queue}
} -result {0}
#>>>
test basic-1.2 {Detect a file creation in a dir} -setup { #<<<
	set queue	[inotify::create_queue]
	fconfigure $queue \
			-blocking 0 \
			-buffering none \
			-translation binary \
			-encoding binary
	set wd	[inotify::add_watch $queue $tmpdir {IN_ALL_EVENTS}]
	set wd_map($wd)	$tmpdir

	set ::tmp	"\n"
	proc readable {fd} {
		if {[eof $fd]} {
			set ::done -2
			return
		}
		set dat	[read $fd]
		set events	[inotify::decode_events $dat]
		set all_done	0
		foreach event $events {
			set wd	[lindex $event 0]
			if {[info exists ::wd_map($wd)]} {
				set event	[lreplace $event 0 0 $::wd_map($wd)]
			}
			append ::tmp "Event:\n\t[join $event \n\t]\n--------------\n"
			if {[lsearch [lindex $event 1] IN_DELETE] != -1} {
				set all_done	1
			}
		}
		if {$all_done} {
			set ::done $::tmp
		}
	}
} -body {
	fileevent $queue readable [list readable $queue]
	set fp	[open [file join $tmpdir foo] a]
	puts $fp "dummy"
	close $fp

	file delete [file join $tmpdir foo]

	after 3000 {set ::done -1}

	vwait ::done
	set ::done
} -cleanup {
	if {[info exists ::tmp]} {
		unset ::tmp
	}
	inotify::rm_watch $queue $wd
	if {[info exists queue]} {close $queue; unset queue}
	rename readable {}
} -result {
Event:
	/lv/home/cyan/git/inotify-tcl/tmp
	IN_CREATE
	0
	16
	foo
--------------
Event:
	/lv/home/cyan/git/inotify-tcl/tmp
	IN_OPEN
	0
	16
	foo
--------------
Event:
	/lv/home/cyan/git/inotify-tcl/tmp
	IN_MODIFY
	0
	16
	foo
--------------
Event:
	/lv/home/cyan/git/inotify-tcl/tmp
	IN_CLOSE_WRITE
	0
	16
	foo
--------------
Event:
	/lv/home/cyan/git/inotify-tcl/tmp
	IN_DELETE
	0
	16
	foo
--------------
}
#>>>

::tcltest::cleanupTests
return
